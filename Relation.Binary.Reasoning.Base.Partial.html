<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Relation.Binary.Reasoning.Base.Partial</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- The basic code for equational reasoning with a non-reflexive relation</a>
<a id="179" class="Comment">------------------------------------------------------------------------</a>

<a id="253" class="Symbol">{-#</a> <a id="257" class="Keyword">OPTIONS</a> <a id="265" class="Pragma">--cubical-compatible</a> <a id="286" class="Pragma">--safe</a> <a id="293" class="Symbol">#-}</a>

<a id="298" class="Keyword">open</a> <a id="303" class="Keyword">import</a> <a id="310" href="Relation.Binary.Core.html" class="Module">Relation.Binary.Core</a>
<a id="331" class="Keyword">open</a> <a id="336" class="Keyword">import</a> <a id="343" href="Relation.Binary.Definitions.html" class="Module">Relation.Binary.Definitions</a>
<a id="371" class="Keyword">open</a> <a id="376" class="Keyword">import</a> <a id="383" href="Level.html" class="Module">Level</a> <a id="389" class="Keyword">using</a> <a id="395" class="Symbol">(</a><a id="396" href="Agda.Primitive.html#961" class="Primitive Operator">_⊔_</a><a id="399" class="Symbol">)</a>
<a id="401" class="Keyword">open</a> <a id="406" class="Keyword">import</a> <a id="413" href="Relation.Binary.PropositionalEquality.Core.html" class="Module">Relation.Binary.PropositionalEquality.Core</a> <a id="456" class="Symbol">as</a> <a id="459" class="Module">P</a>
  <a id="463" class="Keyword">using</a> <a id="469" class="Symbol">(</a><a id="470" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">_≡_</a><a id="473" class="Symbol">)</a>
<a id="475" class="Keyword">open</a> <a id="480" class="Keyword">import</a> <a id="487" href="Relation.Nullary.Decidable.html" class="Module">Relation.Nullary.Decidable</a> <a id="514" class="Keyword">using</a> <a id="520" class="Symbol">(</a><a id="521" href="Relation.Nullary.Decidable.Core.html#1478" class="Record">Dec</a><a id="524" class="Symbol">;</a> <a id="526" href="Relation.Nullary.Decidable.Core.html#1611" class="InductiveConstructor">yes</a><a id="529" class="Symbol">;</a> <a id="531" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a><a id="533" class="Symbol">)</a>
<a id="535" class="Keyword">open</a> <a id="540" class="Keyword">import</a> <a id="547" href="Relation.Nullary.Decidable.html" class="Module">Relation.Nullary.Decidable</a> <a id="574" class="Keyword">using</a> <a id="580" class="Symbol">(</a><a id="581" href="Relation.Nullary.Decidable.Core.html#3409" class="Function">True</a><a id="585" class="Symbol">)</a>

<a id="588" class="Keyword">module</a> <a id="595" href="Relation.Binary.Reasoning.Base.Partial.html" class="Module">Relation.Binary.Reasoning.Base.Partial</a>
  <a id="636" class="Symbol">{</a><a id="637" href="Relation.Binary.Reasoning.Base.Partial.html#637" class="Bound">a</a> <a id="639" href="Relation.Binary.Reasoning.Base.Partial.html#639" class="Bound">ℓ</a><a id="640" class="Symbol">}</a> <a id="642" class="Symbol">{</a><a id="643" href="Relation.Binary.Reasoning.Base.Partial.html#643" class="Bound">A</a> <a id="645" class="Symbol">:</a> <a id="647" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="651" href="Relation.Binary.Reasoning.Base.Partial.html#637" class="Bound">a</a><a id="652" class="Symbol">}</a> <a id="654" class="Symbol">(</a><a id="655" href="Relation.Binary.Reasoning.Base.Partial.html#655" class="Bound Operator">_∼_</a> <a id="659" class="Symbol">:</a> <a id="661" href="Relation.Binary.Core.html#896" class="Function">Rel</a> <a id="665" href="Relation.Binary.Reasoning.Base.Partial.html#643" class="Bound">A</a> <a id="667" href="Relation.Binary.Reasoning.Base.Partial.html#639" class="Bound">ℓ</a><a id="668" class="Symbol">)</a> <a id="670" class="Symbol">(</a><a id="671" href="Relation.Binary.Reasoning.Base.Partial.html#671" class="Bound">trans</a> <a id="677" class="Symbol">:</a> <a id="679" href="Relation.Binary.Definitions.html#2103" class="Function">Transitive</a> <a id="690" href="Relation.Binary.Reasoning.Base.Partial.html#655" class="Bound Operator">_∼_</a><a id="693" class="Symbol">)</a>
  <a id="697" class="Keyword">where</a>

<a id="704" class="Comment">------------------------------------------------------------------------</a>
<a id="777" class="Comment">-- Definition of &quot;related to&quot;</a>

<a id="808" class="Comment">-- This seemingly unnecessary type is used to make it possible to</a>
<a id="874" class="Comment">-- infer arguments even if the underlying equality evaluates.</a>

<a id="937" class="Keyword">infix</a>  <a id="944" class="Number">4</a> <a id="946" href="Relation.Binary.Reasoning.Base.Partial.html#966" class="Datatype Operator">_IsRelatedTo_</a>

<a id="961" class="Keyword">data</a> <a id="_IsRelatedTo_"></a><a id="966" href="Relation.Binary.Reasoning.Base.Partial.html#966" class="Datatype Operator">_IsRelatedTo_</a> <a id="980" class="Symbol">:</a> <a id="982" href="Relation.Binary.Reasoning.Base.Partial.html#643" class="Bound">A</a> <a id="984" class="Symbol">→</a> <a id="986" href="Relation.Binary.Reasoning.Base.Partial.html#643" class="Bound">A</a> <a id="988" class="Symbol">→</a> <a id="990" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="994" class="Symbol">(</a><a id="995" href="Relation.Binary.Reasoning.Base.Partial.html#637" class="Bound">a</a> <a id="997" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="999" href="Relation.Binary.Reasoning.Base.Partial.html#639" class="Bound">ℓ</a><a id="1000" class="Symbol">)</a> <a id="1002" class="Keyword">where</a>
  <a id="_IsRelatedTo_.singleStep"></a><a id="1010" href="Relation.Binary.Reasoning.Base.Partial.html#1010" class="InductiveConstructor">singleStep</a> <a id="1021" class="Symbol">:</a> <a id="1023" class="Symbol">∀</a> <a id="1025" href="Relation.Binary.Reasoning.Base.Partial.html#1025" class="Bound">x</a> <a id="1027" class="Symbol">→</a> <a id="1029" href="Relation.Binary.Reasoning.Base.Partial.html#1025" class="Bound">x</a> <a id="1031" href="Relation.Binary.Reasoning.Base.Partial.html#966" class="Datatype Operator">IsRelatedTo</a> <a id="1043" href="Relation.Binary.Reasoning.Base.Partial.html#1025" class="Bound">x</a>
  <a id="_IsRelatedTo_.multiStep"></a><a id="1047" href="Relation.Binary.Reasoning.Base.Partial.html#1047" class="InductiveConstructor">multiStep</a>  <a id="1058" class="Symbol">:</a> <a id="1060" class="Symbol">∀</a> <a id="1062" class="Symbol">{</a><a id="1063" href="Relation.Binary.Reasoning.Base.Partial.html#1063" class="Bound">x</a> <a id="1065" href="Relation.Binary.Reasoning.Base.Partial.html#1065" class="Bound">y</a><a id="1066" class="Symbol">}</a> <a id="1068" class="Symbol">(</a><a id="1069" href="Relation.Binary.Reasoning.Base.Partial.html#1069" class="Bound">x∼y</a> <a id="1073" class="Symbol">:</a> <a id="1075" href="Relation.Binary.Reasoning.Base.Partial.html#1063" class="Bound">x</a> <a id="1077" href="Relation.Binary.Reasoning.Base.Partial.html#655" class="Bound Operator">∼</a> <a id="1079" href="Relation.Binary.Reasoning.Base.Partial.html#1065" class="Bound">y</a><a id="1080" class="Symbol">)</a> <a id="1082" class="Symbol">→</a> <a id="1084" href="Relation.Binary.Reasoning.Base.Partial.html#1063" class="Bound">x</a> <a id="1086" href="Relation.Binary.Reasoning.Base.Partial.html#966" class="Datatype Operator">IsRelatedTo</a> <a id="1098" href="Relation.Binary.Reasoning.Base.Partial.html#1065" class="Bound">y</a>

<a id="1101" class="Comment">------------------------------------------------------------------------</a>
<a id="1174" class="Comment">-- Types that are used to ensure that the final relation proved by the</a>
<a id="1245" class="Comment">-- chain of reasoning can be converted into the required relation.</a>

<a id="1313" class="Keyword">data</a> <a id="IsMultiStep"></a><a id="1318" href="Relation.Binary.Reasoning.Base.Partial.html#1318" class="Datatype">IsMultiStep</a> <a id="1330" class="Symbol">{</a><a id="1331" href="Relation.Binary.Reasoning.Base.Partial.html#1331" class="Bound">x</a> <a id="1333" href="Relation.Binary.Reasoning.Base.Partial.html#1333" class="Bound">y</a><a id="1334" class="Symbol">}</a> <a id="1336" class="Symbol">:</a> <a id="1338" href="Relation.Binary.Reasoning.Base.Partial.html#1331" class="Bound">x</a> <a id="1340" href="Relation.Binary.Reasoning.Base.Partial.html#966" class="Datatype Operator">IsRelatedTo</a> <a id="1352" href="Relation.Binary.Reasoning.Base.Partial.html#1333" class="Bound">y</a> <a id="1354" class="Symbol">→</a> <a id="1356" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1360" class="Symbol">(</a><a id="1361" href="Relation.Binary.Reasoning.Base.Partial.html#637" class="Bound">a</a> <a id="1363" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1365" href="Relation.Binary.Reasoning.Base.Partial.html#639" class="Bound">ℓ</a><a id="1366" class="Symbol">)</a> <a id="1368" class="Keyword">where</a>
  <a id="IsMultiStep.isMultiStep"></a><a id="1376" href="Relation.Binary.Reasoning.Base.Partial.html#1376" class="InductiveConstructor">isMultiStep</a> <a id="1388" class="Symbol">:</a> <a id="1390" class="Symbol">∀</a> <a id="1392" href="Relation.Binary.Reasoning.Base.Partial.html#1392" class="Bound">x∼y</a> <a id="1396" class="Symbol">→</a> <a id="1398" href="Relation.Binary.Reasoning.Base.Partial.html#1318" class="Datatype">IsMultiStep</a> <a id="1410" class="Symbol">(</a><a id="1411" href="Relation.Binary.Reasoning.Base.Partial.html#1047" class="InductiveConstructor">multiStep</a> <a id="1421" href="Relation.Binary.Reasoning.Base.Partial.html#1392" class="Bound">x∼y</a><a id="1424" class="Symbol">)</a>

<a id="IsMultiStep?"></a><a id="1427" href="Relation.Binary.Reasoning.Base.Partial.html#1427" class="Function">IsMultiStep?</a> <a id="1440" class="Symbol">:</a> <a id="1442" class="Symbol">∀</a> <a id="1444" class="Symbol">{</a><a id="1445" href="Relation.Binary.Reasoning.Base.Partial.html#1445" class="Bound">x</a> <a id="1447" href="Relation.Binary.Reasoning.Base.Partial.html#1447" class="Bound">y</a><a id="1448" class="Symbol">}</a> <a id="1450" class="Symbol">(</a><a id="1451" href="Relation.Binary.Reasoning.Base.Partial.html#1451" class="Bound">x∼y</a> <a id="1455" class="Symbol">:</a> <a id="1457" href="Relation.Binary.Reasoning.Base.Partial.html#1445" class="Bound">x</a> <a id="1459" href="Relation.Binary.Reasoning.Base.Partial.html#966" class="Datatype Operator">IsRelatedTo</a> <a id="1471" href="Relation.Binary.Reasoning.Base.Partial.html#1447" class="Bound">y</a><a id="1472" class="Symbol">)</a> <a id="1474" class="Symbol">→</a> <a id="1476" href="Relation.Nullary.Decidable.Core.html#1478" class="Record">Dec</a> <a id="1480" class="Symbol">(</a><a id="1481" href="Relation.Binary.Reasoning.Base.Partial.html#1318" class="Datatype">IsMultiStep</a> <a id="1493" href="Relation.Binary.Reasoning.Base.Partial.html#1451" class="Bound">x∼y</a><a id="1496" class="Symbol">)</a>
<a id="1498" href="Relation.Binary.Reasoning.Base.Partial.html#1427" class="Function">IsMultiStep?</a> <a id="1511" class="Symbol">(</a><a id="1512" href="Relation.Binary.Reasoning.Base.Partial.html#1047" class="InductiveConstructor">multiStep</a> <a id="1522" href="Relation.Binary.Reasoning.Base.Partial.html#1522" class="Bound">x&lt;y</a><a id="1525" class="Symbol">)</a> <a id="1527" class="Symbol">=</a> <a id="1529" href="Relation.Nullary.Decidable.Core.html#1611" class="InductiveConstructor">yes</a> <a id="1533" class="Symbol">(</a><a id="1534" href="Relation.Binary.Reasoning.Base.Partial.html#1376" class="InductiveConstructor">isMultiStep</a> <a id="1546" href="Relation.Binary.Reasoning.Base.Partial.html#1522" class="Bound">x&lt;y</a><a id="1549" class="Symbol">)</a>
<a id="1551" href="Relation.Binary.Reasoning.Base.Partial.html#1427" class="Function">IsMultiStep?</a> <a id="1564" class="Symbol">(</a><a id="1565" href="Relation.Binary.Reasoning.Base.Partial.html#1010" class="InductiveConstructor">singleStep</a> <a id="1576" class="Symbol">_)</a>  <a id="1580" class="Symbol">=</a> <a id="1582" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="1585" class="Symbol">λ()</a>

<a id="1590" class="Comment">------------------------------------------------------------------------</a>
<a id="1663" class="Comment">-- Reasoning combinators</a>

<a id="1689" class="Comment">-- Note that the arguments to the `step`s are not provided in their</a>
<a id="1757" class="Comment">-- &quot;natural&quot; order and syntax declarations are later used to re-order</a>
<a id="1827" class="Comment">-- them. This is because the `step` ordering allows the type-checker to</a>
<a id="1899" class="Comment">-- better infer the middle argument `y` from the `_IsRelatedTo_`</a>
<a id="1964" class="Comment">-- argument (see issue 622).</a>
<a id="1993" class="Comment">--</a>
<a id="1996" class="Comment">-- This has two practical benefits. First it speeds up type-checking by</a>
<a id="2068" class="Comment">-- approximately a factor of 5. Secondly it allows the combinators to be</a>
<a id="2141" class="Comment">-- used with macros that use reflection, e.g. `Tactic.RingSolver`, where</a>
<a id="2214" class="Comment">-- they need to be able to extract `y` using reflection.</a>

<a id="2272" class="Keyword">infix</a>  <a id="2279" class="Number">1</a> <a id="2281" href="Relation.Binary.Reasoning.Base.Partial.html#2378" class="Function Operator">begin_</a>
<a id="2288" class="Keyword">infixr</a> <a id="2295" class="Number">2</a> <a id="2297" href="Relation.Binary.Reasoning.Base.Partial.html#2520" class="Function">step-∼</a> <a id="2304" href="Relation.Binary.Reasoning.Base.Partial.html#2737" class="Function">step-≡</a> <a id="2311" href="Relation.Binary.Reasoning.Base.Partial.html#2886" class="Function">step-≡˘</a>
<a id="2319" class="Keyword">infixr</a> <a id="2326" class="Number">2</a> <a id="2328" href="Relation.Binary.Reasoning.Base.Partial.html#3028" class="Function Operator">_≡⟨⟩_</a>
<a id="2334" class="Keyword">infix</a>  <a id="2341" class="Number">3</a> <a id="2343" href="Relation.Binary.Reasoning.Base.Partial.html#3585" class="Function Operator">_∎⟨_⟩</a> <a id="2349" href="Relation.Binary.Reasoning.Base.Partial.html#3113" class="Function Operator">_∎</a>

<a id="2353" class="Comment">-- Beginning of a proof</a>

<a id="begin_"></a><a id="2378" href="Relation.Binary.Reasoning.Base.Partial.html#2378" class="Function Operator">begin_</a> <a id="2385" class="Symbol">:</a> <a id="2387" class="Symbol">∀</a> <a id="2389" class="Symbol">{</a><a id="2390" href="Relation.Binary.Reasoning.Base.Partial.html#2390" class="Bound">x</a> <a id="2392" href="Relation.Binary.Reasoning.Base.Partial.html#2392" class="Bound">y</a><a id="2393" class="Symbol">}</a> <a id="2395" class="Symbol">(</a><a id="2396" href="Relation.Binary.Reasoning.Base.Partial.html#2396" class="Bound">x∼y</a> <a id="2400" class="Symbol">:</a> <a id="2402" href="Relation.Binary.Reasoning.Base.Partial.html#2390" class="Bound">x</a> <a id="2404" href="Relation.Binary.Reasoning.Base.Partial.html#966" class="Datatype Operator">IsRelatedTo</a> <a id="2416" href="Relation.Binary.Reasoning.Base.Partial.html#2392" class="Bound">y</a><a id="2417" class="Symbol">)</a> <a id="2419" class="Symbol">→</a> <a id="2421" class="Symbol">{</a><a id="2422" href="Relation.Nullary.Decidable.Core.html#3409" class="Function">True</a> <a id="2427" class="Symbol">(</a><a id="2428" href="Relation.Binary.Reasoning.Base.Partial.html#1427" class="Function">IsMultiStep?</a> <a id="2441" href="Relation.Binary.Reasoning.Base.Partial.html#2396" class="Bound">x∼y</a><a id="2444" class="Symbol">)}</a> <a id="2447" class="Symbol">→</a> <a id="2449" href="Relation.Binary.Reasoning.Base.Partial.html#2390" class="Bound">x</a> <a id="2451" href="Relation.Binary.Reasoning.Base.Partial.html#655" class="Bound Operator">∼</a> <a id="2453" href="Relation.Binary.Reasoning.Base.Partial.html#2392" class="Bound">y</a>
<a id="2455" href="Relation.Binary.Reasoning.Base.Partial.html#2378" class="Function Operator">begin</a> <a id="2461" class="Symbol">(</a><a id="2462" href="Relation.Binary.Reasoning.Base.Partial.html#1047" class="InductiveConstructor">multiStep</a> <a id="2472" href="Relation.Binary.Reasoning.Base.Partial.html#2472" class="Bound">x∼y</a><a id="2475" class="Symbol">)</a> <a id="2477" class="Symbol">=</a> <a id="2479" href="Relation.Binary.Reasoning.Base.Partial.html#2472" class="Bound">x∼y</a>

<a id="2484" class="Comment">-- Standard step with the relation</a>

<a id="step-∼"></a><a id="2520" href="Relation.Binary.Reasoning.Base.Partial.html#2520" class="Function">step-∼</a> <a id="2527" class="Symbol">:</a> <a id="2529" class="Symbol">∀</a> <a id="2531" href="Relation.Binary.Reasoning.Base.Partial.html#2531" class="Bound">x</a> <a id="2533" class="Symbol">{</a><a id="2534" href="Relation.Binary.Reasoning.Base.Partial.html#2534" class="Bound">y</a> <a id="2536" href="Relation.Binary.Reasoning.Base.Partial.html#2536" class="Bound">z</a><a id="2537" class="Symbol">}</a> <a id="2539" class="Symbol">→</a> <a id="2541" href="Relation.Binary.Reasoning.Base.Partial.html#2534" class="Bound">y</a> <a id="2543" href="Relation.Binary.Reasoning.Base.Partial.html#966" class="Datatype Operator">IsRelatedTo</a> <a id="2555" href="Relation.Binary.Reasoning.Base.Partial.html#2536" class="Bound">z</a> <a id="2557" class="Symbol">→</a> <a id="2559" href="Relation.Binary.Reasoning.Base.Partial.html#2531" class="Bound">x</a> <a id="2561" href="Relation.Binary.Reasoning.Base.Partial.html#655" class="Bound Operator">∼</a> <a id="2563" href="Relation.Binary.Reasoning.Base.Partial.html#2534" class="Bound">y</a> <a id="2565" class="Symbol">→</a> <a id="2567" href="Relation.Binary.Reasoning.Base.Partial.html#2531" class="Bound">x</a> <a id="2569" href="Relation.Binary.Reasoning.Base.Partial.html#966" class="Datatype Operator">IsRelatedTo</a> <a id="2581" href="Relation.Binary.Reasoning.Base.Partial.html#2536" class="Bound">z</a>
<a id="2583" href="Relation.Binary.Reasoning.Base.Partial.html#2520" class="Function">step-∼</a> <a id="2590" class="Symbol">_</a> <a id="2592" class="Symbol">(</a><a id="2593" href="Relation.Binary.Reasoning.Base.Partial.html#1010" class="InductiveConstructor">singleStep</a> <a id="2604" class="Symbol">_)</a> <a id="2607" href="Relation.Binary.Reasoning.Base.Partial.html#2607" class="Bound">x∼y</a>  <a id="2612" class="Symbol">=</a> <a id="2614" href="Relation.Binary.Reasoning.Base.Partial.html#1047" class="InductiveConstructor">multiStep</a> <a id="2624" href="Relation.Binary.Reasoning.Base.Partial.html#2607" class="Bound">x∼y</a>
<a id="2628" href="Relation.Binary.Reasoning.Base.Partial.html#2520" class="Function">step-∼</a> <a id="2635" class="Symbol">_</a> <a id="2637" class="Symbol">(</a><a id="2638" href="Relation.Binary.Reasoning.Base.Partial.html#1047" class="InductiveConstructor">multiStep</a> <a id="2648" href="Relation.Binary.Reasoning.Base.Partial.html#2648" class="Bound">y∼z</a><a id="2651" class="Symbol">)</a> <a id="2653" href="Relation.Binary.Reasoning.Base.Partial.html#2653" class="Bound">x∼y</a> <a id="2657" class="Symbol">=</a> <a id="2659" href="Relation.Binary.Reasoning.Base.Partial.html#1047" class="InductiveConstructor">multiStep</a> <a id="2669" class="Symbol">(</a><a id="2670" href="Relation.Binary.Reasoning.Base.Partial.html#671" class="Bound">trans</a> <a id="2676" href="Relation.Binary.Reasoning.Base.Partial.html#2653" class="Bound">x∼y</a> <a id="2680" href="Relation.Binary.Reasoning.Base.Partial.html#2648" class="Bound">y∼z</a><a id="2683" class="Symbol">)</a>

<a id="2686" class="Comment">-- Step with a non-trivial propositional equality</a>

<a id="step-≡"></a><a id="2737" href="Relation.Binary.Reasoning.Base.Partial.html#2737" class="Function">step-≡</a> <a id="2744" class="Symbol">:</a> <a id="2746" class="Symbol">∀</a> <a id="2748" href="Relation.Binary.Reasoning.Base.Partial.html#2748" class="Bound">x</a> <a id="2750" class="Symbol">{</a><a id="2751" href="Relation.Binary.Reasoning.Base.Partial.html#2751" class="Bound">y</a> <a id="2753" href="Relation.Binary.Reasoning.Base.Partial.html#2753" class="Bound">z</a><a id="2754" class="Symbol">}</a> <a id="2756" class="Symbol">→</a> <a id="2758" href="Relation.Binary.Reasoning.Base.Partial.html#2751" class="Bound">y</a> <a id="2760" href="Relation.Binary.Reasoning.Base.Partial.html#966" class="Datatype Operator">IsRelatedTo</a> <a id="2772" href="Relation.Binary.Reasoning.Base.Partial.html#2753" class="Bound">z</a> <a id="2774" class="Symbol">→</a> <a id="2776" href="Relation.Binary.Reasoning.Base.Partial.html#2748" class="Bound">x</a> <a id="2778" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="2780" href="Relation.Binary.Reasoning.Base.Partial.html#2751" class="Bound">y</a> <a id="2782" class="Symbol">→</a> <a id="2784" href="Relation.Binary.Reasoning.Base.Partial.html#2748" class="Bound">x</a> <a id="2786" href="Relation.Binary.Reasoning.Base.Partial.html#966" class="Datatype Operator">IsRelatedTo</a> <a id="2798" href="Relation.Binary.Reasoning.Base.Partial.html#2753" class="Bound">z</a>
<a id="2800" href="Relation.Binary.Reasoning.Base.Partial.html#2737" class="Function">step-≡</a> <a id="2807" class="Symbol">_</a> <a id="2809" href="Relation.Binary.Reasoning.Base.Partial.html#2809" class="Bound">x∼z</a> <a id="2813" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">P.refl</a> <a id="2820" class="Symbol">=</a> <a id="2822" href="Relation.Binary.Reasoning.Base.Partial.html#2809" class="Bound">x∼z</a>

<a id="2827" class="Comment">-- Step with a flipped non-trivial propositional equality</a>

<a id="step-≡˘"></a><a id="2886" href="Relation.Binary.Reasoning.Base.Partial.html#2886" class="Function">step-≡˘</a> <a id="2894" class="Symbol">:</a> <a id="2896" class="Symbol">∀</a> <a id="2898" href="Relation.Binary.Reasoning.Base.Partial.html#2898" class="Bound">x</a> <a id="2900" class="Symbol">{</a><a id="2901" href="Relation.Binary.Reasoning.Base.Partial.html#2901" class="Bound">y</a> <a id="2903" href="Relation.Binary.Reasoning.Base.Partial.html#2903" class="Bound">z</a><a id="2904" class="Symbol">}</a> <a id="2906" class="Symbol">→</a> <a id="2908" href="Relation.Binary.Reasoning.Base.Partial.html#2901" class="Bound">y</a> <a id="2910" href="Relation.Binary.Reasoning.Base.Partial.html#966" class="Datatype Operator">IsRelatedTo</a> <a id="2922" href="Relation.Binary.Reasoning.Base.Partial.html#2903" class="Bound">z</a> <a id="2924" class="Symbol">→</a> <a id="2926" href="Relation.Binary.Reasoning.Base.Partial.html#2901" class="Bound">y</a> <a id="2928" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="2930" href="Relation.Binary.Reasoning.Base.Partial.html#2898" class="Bound">x</a> <a id="2932" class="Symbol">→</a> <a id="2934" href="Relation.Binary.Reasoning.Base.Partial.html#2898" class="Bound">x</a> <a id="2936" href="Relation.Binary.Reasoning.Base.Partial.html#966" class="Datatype Operator">IsRelatedTo</a> <a id="2948" href="Relation.Binary.Reasoning.Base.Partial.html#2903" class="Bound">z</a>
<a id="2950" href="Relation.Binary.Reasoning.Base.Partial.html#2886" class="Function">step-≡˘</a> <a id="2958" class="Symbol">_</a> <a id="2960" href="Relation.Binary.Reasoning.Base.Partial.html#2960" class="Bound">x∼z</a> <a id="2964" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">P.refl</a> <a id="2971" class="Symbol">=</a> <a id="2973" href="Relation.Binary.Reasoning.Base.Partial.html#2960" class="Bound">x∼z</a>

<a id="2978" class="Comment">-- -- Step with a trivial propositional equality</a>

<a id="_≡⟨⟩_"></a><a id="3028" href="Relation.Binary.Reasoning.Base.Partial.html#3028" class="Function Operator">_≡⟨⟩_</a> <a id="3034" class="Symbol">:</a> <a id="3036" class="Symbol">∀</a> <a id="3038" href="Relation.Binary.Reasoning.Base.Partial.html#3038" class="Bound">x</a> <a id="3040" class="Symbol">{</a><a id="3041" href="Relation.Binary.Reasoning.Base.Partial.html#3041" class="Bound">y</a><a id="3042" class="Symbol">}</a> <a id="3044" class="Symbol">→</a> <a id="3046" href="Relation.Binary.Reasoning.Base.Partial.html#3038" class="Bound">x</a> <a id="3048" href="Relation.Binary.Reasoning.Base.Partial.html#966" class="Datatype Operator">IsRelatedTo</a> <a id="3060" href="Relation.Binary.Reasoning.Base.Partial.html#3041" class="Bound">y</a> <a id="3062" class="Symbol">→</a> <a id="3064" href="Relation.Binary.Reasoning.Base.Partial.html#3038" class="Bound">x</a> <a id="3066" href="Relation.Binary.Reasoning.Base.Partial.html#966" class="Datatype Operator">IsRelatedTo</a> <a id="3078" href="Relation.Binary.Reasoning.Base.Partial.html#3041" class="Bound">y</a>
<a id="3080" class="Symbol">_</a> <a id="3082" href="Relation.Binary.Reasoning.Base.Partial.html#3028" class="Function Operator">≡⟨⟩</a> <a id="3086" href="Relation.Binary.Reasoning.Base.Partial.html#3086" class="Bound">x∼y</a> <a id="3090" class="Symbol">=</a> <a id="3092" href="Relation.Binary.Reasoning.Base.Partial.html#3086" class="Bound">x∼y</a>

<a id="3097" class="Comment">-- Termination</a>

<a id="_∎"></a><a id="3113" href="Relation.Binary.Reasoning.Base.Partial.html#3113" class="Function Operator">_∎</a> <a id="3116" class="Symbol">:</a> <a id="3118" class="Symbol">∀</a> <a id="3120" href="Relation.Binary.Reasoning.Base.Partial.html#3120" class="Bound">x</a> <a id="3122" class="Symbol">→</a> <a id="3124" href="Relation.Binary.Reasoning.Base.Partial.html#3120" class="Bound">x</a> <a id="3126" href="Relation.Binary.Reasoning.Base.Partial.html#966" class="Datatype Operator">IsRelatedTo</a> <a id="3138" href="Relation.Binary.Reasoning.Base.Partial.html#3120" class="Bound">x</a>
<a id="3140" href="Relation.Binary.Reasoning.Base.Partial.html#3113" class="Function Operator">_∎</a> <a id="3143" class="Symbol">=</a> <a id="3145" href="Relation.Binary.Reasoning.Base.Partial.html#1010" class="InductiveConstructor">singleStep</a>

<a id="3157" class="Comment">-- Syntax declarations</a>

<a id="3181" class="Keyword">syntax</a> <a id="3188" href="Relation.Binary.Reasoning.Base.Partial.html#2520" class="Function">step-∼</a>  <a id="3196" class="Bound">x</a> <a id="3198" class="Bound">y∼z</a> <a id="3202" class="Bound">x∼y</a> <a id="3206" class="Symbol">=</a> <a id="3208" class="Bound">x</a> <a id="3210" class="Function">∼⟨</a>  <a id="3214" class="Bound">x∼y</a> <a id="3218" class="Function">⟩</a> <a id="3220" class="Bound">y∼z</a>
<a id="3224" class="Keyword">syntax</a> <a id="3231" href="Relation.Binary.Reasoning.Base.Partial.html#2737" class="Function">step-≡</a>  <a id="3239" class="Bound">x</a> <a id="3241" class="Bound">y≡z</a> <a id="3245" class="Bound">x≡y</a> <a id="3249" class="Symbol">=</a> <a id="3251" class="Bound">x</a> <a id="3253" class="Function">≡⟨</a>  <a id="3257" class="Bound">x≡y</a> <a id="3261" class="Function">⟩</a> <a id="3263" class="Bound">y≡z</a>
<a id="3267" class="Keyword">syntax</a> <a id="3274" href="Relation.Binary.Reasoning.Base.Partial.html#2886" class="Function">step-≡˘</a> <a id="3282" class="Bound">x</a> <a id="3284" class="Bound">y≡z</a> <a id="3288" class="Bound">y≡x</a> <a id="3292" class="Symbol">=</a> <a id="3294" class="Bound">x</a> <a id="3296" class="Function">≡˘⟨</a> <a id="3300" class="Bound">y≡x</a> <a id="3304" class="Function">⟩</a> <a id="3306" class="Bound">y≡z</a>


<a id="3312" class="Comment">------------------------------------------------------------------------</a>
<a id="3385" class="Comment">-- DEPRECATED NAMES</a>
<a id="3405" class="Comment">------------------------------------------------------------------------</a>
<a id="3478" class="Comment">-- Please use the new names as continuing support for the old names is</a>
<a id="3549" class="Comment">-- not guaranteed.</a>

<a id="3569" class="Comment">-- Version 1.6</a>

<a id="_∎⟨_⟩"></a><a id="3585" href="Relation.Binary.Reasoning.Base.Partial.html#3585" class="Function Operator">_∎⟨_⟩</a> <a id="3591" class="Symbol">:</a> <a id="3593" class="Symbol">∀</a> <a id="3595" href="Relation.Binary.Reasoning.Base.Partial.html#3595" class="Bound">x</a> <a id="3597" class="Symbol">→</a> <a id="3599" href="Relation.Binary.Reasoning.Base.Partial.html#3595" class="Bound">x</a> <a id="3601" href="Relation.Binary.Reasoning.Base.Partial.html#655" class="Bound Operator">∼</a> <a id="3603" href="Relation.Binary.Reasoning.Base.Partial.html#3595" class="Bound">x</a> <a id="3605" class="Symbol">→</a> <a id="3607" href="Relation.Binary.Reasoning.Base.Partial.html#3595" class="Bound">x</a> <a id="3609" href="Relation.Binary.Reasoning.Base.Partial.html#966" class="Datatype Operator">IsRelatedTo</a> <a id="3621" href="Relation.Binary.Reasoning.Base.Partial.html#3595" class="Bound">x</a>
<a id="3623" class="Symbol">_</a> <a id="3625" href="Relation.Binary.Reasoning.Base.Partial.html#3585" class="Function Operator">∎⟨</a> <a id="3628" href="Relation.Binary.Reasoning.Base.Partial.html#3628" class="Bound">x∼x</a> <a id="3632" href="Relation.Binary.Reasoning.Base.Partial.html#3585" class="Function Operator">⟩</a> <a id="3634" class="Symbol">=</a> <a id="3636" href="Relation.Binary.Reasoning.Base.Partial.html#1047" class="InductiveConstructor">multiStep</a> <a id="3646" href="Relation.Binary.Reasoning.Base.Partial.html#3628" class="Bound">x∼x</a>
<a id="3650" class="Symbol">{-#</a> <a id="3654" class="Keyword">WARNING_ON_USAGE</a> <a id="3671" class="Pragma">_∎⟨_⟩</a>
<a id="3677" class="String">&quot;Warning: _∎⟨_⟩ was deprecated in v1.6.
Please use _∎ instead if used in a chain, otherwise simply provide
the proof of reflexivity directly without using these combinators.&quot;</a>
<a id="3852" class="Symbol">#-}</a>
</pre></body></html>